
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ГЛН = Параметры.СтруктураПараметров.ILN;
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.СтруктураПараметров);
	
	Элементы.Партнер.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка." + Параметры.СтруктураПараметров.ОграничениеТипа);
	
	Заголовок = ГЛН + "--" + Наименование;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Оповещение = Новый ОписаниеОповещения("ЗакрытиеФормы", ЭтотОбъект, Новый Структура);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Модифицированность = Ложь;
	Закрыть(СформироватьСтруктуруВозврата());
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытиеФормы(Ответ, ДополнительныеПараметры) Экспорт
	
	Модифицированность = Ложь;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Закрыть(СформироватьСтруктуруВозврата());
	Иначе
		Закрыть();
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьСтруктуруВозврата()
	
	СтруктураПартнера = Новый Структура("FNSID, Партнер, СписокВходящих, СписокИсходящих");
	ЗаполнитьЗначенияСвойств(СтруктураПартнера, ЭтаФорма);
	
	Возврат СтруктураПартнера;

КонецФункции

#КонецОбласти
